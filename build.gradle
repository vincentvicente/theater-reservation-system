plugins {
// Build Java: https://docs.gradle.org/current/userguide/building_java_projects.html
    id 'java'

// Spring Boot plugin
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'

// https://docs.gradle.org/current/userguide/pmd_plugin.html#header
    id 'pmd'

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
    id 'jacoco'
}

group 'CS 5004 - Summer 2024'
version '1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot dependencies
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.h2database:h2'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    
    // CORS support
    implementation 'org.springframework.boot:spring-boot-starter-web'
    
    // Existing dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

pmd {
    ignoreFailures=true
    pmdTest.enabled=false
    ruleSets = [
            "category/java/bestpractices.xml",
            "category/java/errorprone.xml",
            "category/java/codestyle.xml"
            //"java-basic",
            //"java-braces",
            //"java-strings",
    ]
}

javadoc {
    doLast {
        String fixedBuildDir = buildDir.toString().replace('\\', '/')
        println "file:///$fixedBuildDir/docs/javadoc/index.html"
    }
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    reports {
        html.destination file("${buildDir}/jacocoHtml")
    }
    doLast {
        String fixedBuildDir = buildDir.toString().replace('\\', '/')
        println "file:///$fixedBuildDir/jacocoHtml/index.html"
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                // minimum percentage of code coverage
                minimum = 0.2
            }
        }
        rule {
            enabled = false
            element = 'CLASS'
            includes = ['org.gradle.*']
            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 0.3
            }
        }
    }
}

// Fail the build if code coverage isn't high enough
check.dependsOn jacocoTestCoverageVerification

// Run code coverage after tests run
jacocoTestReport.mustRunAfter test


task doAll {
    // Runs a java build. Imported from the java plugin
    dependsOn build
    // Runs all the tests. Imported from the java plugin.
    dependsOn test
    // Runs pmd. Imported by pmd plugin
    dependsOn check
    // Runs java doc. Imported from the java plugin
    dependsOn javadoc
    println 'all done!'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
